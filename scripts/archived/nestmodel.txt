
model {
    
#####LIKELIHOOD
for(i in 1:nind){  
  for(t in (first[i]+1):last[i]){
    
    enc.hist[i,t] ~ dbern(S[i,t-1]*enc.hist[i,t-1])   
    
    #model for daily survival  
    logit(S[i,t-1]) <- alpha.Pr[pair.ID[i]] + alpha.Treat[egg.treatment[i]]
    
    + beta.fac[facility[i]] + beta.temp.mn*temp.mn[i,t-1] 

    temp.mn[i,t] ~ dnorm(mn.temp.mn[i,t],tau.temp.mn[egg.treatment[i]])

    mn.temp.mn[i,t] <- mu.mn.temp + rho.mn.temp*(temp.mn[i,t-1]-mu.mn.temp)

  }
}
    
#PARAMETER PRIORS 
beta.int ~ dnorm(0,0.001)
beta.temp.mn ~ dnorm(0,0.001)

for(f in 1:8){
  tau.temp.mn[f] <- pow(sigma.temp.mn[f],-2)
  sigma.temp.mn[f] ~ dunif(0,5)
}

mu.mn.temp ~ dnorm(0,0.001)
rho.mn.temp ~ dunif(-1,0)

for(f in 1:2){
  beta.fac[f] ~ dnorm(0,0.001)
}
beta.fac[3] <- 0

#RANDOM EFFECT
for(i in 1:npair){
  alpha.Pr[i] ~ dnorm(beta.int,tau.pair)
}
tau.pair <- pow(sigma.pair,-2)
sigma.pair ~ dunif(0,25) 

for(i in 1:ntreat){
  alpha.Treat[i] ~ dnorm(0,tau.treat)
}
tau.treat <- pow(sigma.treat,-2)
sigma.treat ~ dunif(0,25) 

}
